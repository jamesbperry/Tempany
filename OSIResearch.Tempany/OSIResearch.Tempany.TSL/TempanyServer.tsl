TrinitySettings
{
	RootNamespace: OSIResearch.Tempany.Generated;
}

server TempanyServer
{
	protocol InsertStreamValue;
	protocol GetStreamValue;
	protocol GetStreamValues;
	protocol CreateStream;
	
	protocol CreateCellStream;
	protocol GetCellStreamValue;
	protocol InsertCellStreamValue;	
}

protocol CreateStream
{
	Type:Syn;
	Request:CreateStreamArgs;
	Response:CellIdentifier;
}

protocol InsertStreamValue
{
	Type:Syn;
	Request:StreamValueCollectionDTO;
	Response:void;
}

protocol GetStreamValue
{
	Type:Syn;
	Request:GetStreamValueArgs;
	Response:StreamValueCollectionDTO;
}

protocol GetStreamValues
{
	Type:Syn;
	Request:GetStreamValuesArgs;
	Response:StreamValueCollectionsDTO;	
}

protocol CreateCellStream
{
	Type:Syn;
	Request:CreateCellStreamArgs;
	Response:CellIdentifier;
}

protocol GetCellStreamValue
{
	Type:Syn;
	Request:GetCellStreamValueArgs;
	Response:CellStreamValueCollectionDTO;
}

protocol InsertCellStreamValue
{
	Type:Syn;
	Request:InsertCellStreamValueArgs;
	Response:void;
}

protocol GetMorphon
{
	Type:Syn;
	Request:GetMorphonArgs;
	Response:MorphonDTO;	
}

struct CreateCellStreamArgs
{
	
}

struct InsertCellStreamValueArgs
{
	long CellStream;	
	long Ticks;
	long Morphon;
	bool IsMember;
}

struct GetCellStreamValueArgs
{
	long CellStreamId;
	long Ticks;
}

struct CellStreamValueCollectionsDTO
{
	List<CellStreamValueCollectionDTO> ValueCollections;
}

struct CellStreamValueCollectionDTO
{
	long Ticks;
	long CellStreamId;
	List<long> Members;
}

struct SearchMorphonsArgs
{
	
}

struct GetMorphonArgs
{
	optional string MorphonType;
	optional long MorphonCellId;
}

struct MorphonDTO
{
	List<NamedStreamDTO> NamedStreams;
}

struct NamedStreamDTO
{
	string Name;
	bool IsIdentifier;
	long SnapshotCellId;
	byte ValueType;
	optional string ValueTypeDiscriminator;	
}

struct GetStreamValueArgs
{
	long StreamCellId;
	long Timestamp;	
}

struct GetStreamValuesArgs
{
	long StreamCellId;
	long StartTime;
	long EndTime;
	byte RetrievalMode;
}

struct StreamValueCollectionsDTO
{
	List<StreamValueCollectionDTO> ValueCollections;
}

struct StreamValueCollectionDTO
{
	List<StreamValueDTO> Values;
	long Timestamp;
	long StreamCellId;
}

struct StreamValueDTO
{	
	optional long CellValue;
	optional double NumericValue;
	optional string StringValue;
	optional long DigitalValue;
}

struct CreateStreamArgs
{
	byte ValueType;
	optional string ValueTypeDiscriminator;
}

struct CellIdentifier
{
	long Id;	
}

struct TimestampPointerDTO
{
	long Id;	
	long Ticks;
	long Values;
	List<long> Previous;
}

cell struct Morphon
{
	List<CellId> NamedStreams;
	List<CellId> MemberOf;
}

cell struct NamedStream
{
	string Name;
	bool IsIdentifier;
	CellId TempanyStream;
}

cell struct TempanyStream
{
	CellId Snapshot;
	byte ValueType;
	optional string ValueTypeDiscriminator;
	int IndexHeight;
}

cell struct TempanyCellCollectionStream
{
	long Id;
	List<CellId> Streams;
}

cell struct TempanyCellStream
{
	long Id;
	CellId Collection;
	CellId Morphon;
	TempanyCellStreamValue Snapshot;
	List<TempanyCellStreamValue> Values;
}

struct TempanyCellStreamValue
{
	bool IsMember;
	long Ticks;
}

cell struct Timestamp
{
	long Ticks;
	CellId Values;
	List<CellId> Previous;
}

cell struct StreamValueCollection
{
	List<CellId> Values;
}

cell struct StreamValue
{	
	optional long CellValue;
	optional double NumericValue;
	optional string StringValue;
	optional long DigitalValue;
}

cell struct DigitalSet
{
	string Name;	
	List<CellId> DigitalValues;
}

cell struct DigitalValue
{
	long DigitalSet;
	int Value;
	string Name;	
}